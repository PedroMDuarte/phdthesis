reset
f="/lab/data/app3/2011/1109/110908/l_evaporation_330G_tau1400.EVAPDAT_ANALYSIS_SEPT_20"
g="/lab/data/app3/2011/1109/110908/l_evaporation_330G_tau1400.EVAPTRAJ"
#set logscale y
#set logscale y2

set xr  [0:6.5]
set yr  [0:70]
set y2r [2:7]

set ytics format "" nomirror
set ytics 0, 10, 70
set ytics add ("\\sf 0" 0, "\\sf 10" 10, "\\sf 20" 20, "\\sf 30" 30, "\\sf 40" 40, "\\sf 50" 50, "\\sf 60" 60)
set mytics 2

set y2tics format "" nomirror
set y2tics 3,1,8 
set y2tics add ("\\sf 2" 2, "\\sf 3" 3, "\\sf 4" 4, "\\sf 5" 5, "\\sf 6" 6)  
set my2tics 2

set xtics format ""
set xtics 0,1,7
set xtics add ("\\sf 0" 0, "\\sf 1" 1, "\\sf 2" 2, "\\sf 3" 3, "\\sf 4" 4, "\\sf 5" 5, "\\sf 6" 6, "\\sf 7" 7)
set mxtics 2

set xl "\\sf Time (s)" offset 0,-0.75
set yl "\\sf {\\itshape T} ($\\mu$K)" offset 4.3, 0
set y2l "\\sf {\\itshape N} ($\\mathsf{10^{6}}$)" offset -4.3, 0

unset key

set arrow 1 from 1.5, 15.86 to 0.2, 15.86 lc rgbcolor "blue" lw 0.5 
set arrow 2 from 4.4, second 3.46 to 6.2, second 3.46 lc rgbcolor "forest-green" lw 0.5 

#set grid lc rgbcolor "#AAAAAA"

set terminal epslatex size 4.2,3.3 color lw 3
set output "evap.tex"

set multiplot 

set size 1.,0.7
set origin 0.08,0

plot \
f u 1:($1<0.5? $4 : 1/0):5 w yerrorbars pt 5 lt 1 lc rgbcolor "blue" notitle ,\
f u 1:($1>0.5 && $1 <3.0? $4 : 1/0):($4*0.05) w yerrorbars pt 5 lt 1 lc rgbcolor "blue" notitle ,\
f u 1:($1 >3.0? $10*$3 : 1/0):($11*$3) w yerrorbars pt 5 lt 1 lc rgbcolor "blue" notitle ,\
f u 1:(2*$7):(2*$9) pt 7 lt 1 lc rgbcolor "forest-green" w yerrorbars axes x1y2 notitle ,\
g u 1:($2/5) lt 2 lc rgbcolor "dark-blue" w lines  notitle 

set object 1 rectangle from 0.35,0.25 to 0.85,0.65 fillcolor rgb "green" front 

set size 0.74,0.35
set origin 0.14, 0.32

set border lw 0.5 

unset arrow 1
unset arrow 2

set xr [2.8:7]
set yr [0:2.9]
set ytics 
unset y2tics
set xl "\\scriptsize \\sf {\\itshape N} ($\\mathsf{10^{6}}$)" offset 0, 0.3
set yl "\\scriptsize \\sf \\itshape T/T$_{\\mathsf F}$"  offset 17.5,0 
unset y2l

set ytics format ""
set ytics 0,1,3
set ytics offset 0.5,0 add ("\\scriptsize \\sf 0" 0, "\\scriptsize \\sf 1" 1, "\\scriptsize \\sf 2" 2)
set mytics 2

set xtics format ""
set xtics 3,1,7
set xtics offset 0, 0.5 add ("\\scriptsize \\sf 2" 2, "\\scriptsize \\sf 3" 3, "\\scriptsize \\sf 4" 4, "\\scriptsize \\sf 5" 5, "\\scriptsize \\sf 6" 6, "\\scriptsize \\sf 7" 7)
set mxtics 2 

unset grid 


plot \
f u (2*$7):(2*$7>4.?$6:1/0):(2*$9):($5/$3) w xyerrorbars ps 1.2 pt 5 lc rgbcolor "blue" notitle ,\
f u (2*$7):(2*$7>3.? $20>0.1? 2*$7<4.? $10 :1/0 :1/0 :1/0):(2*$9):($11) w xyerrorbars ps 1.0 pt 5 lt 1 lc rgbcolor "blue" notitle
#f u (2*$7/1.e6):(2*$7/1.e6>4.5? $10>0.1?$10:1/0 :1/0):11 ps 1.0 pt 2 lt 1 lc rgbcolor "blue" w yerrorbars notitle




