reset
set fit errorvariables

f="/lab/data/app3/2011/1109/110920/uvls_retake.LSDAT"
xs=1.0 #x-scale is 1.0 MHz/ MHz
is= 1.

# This gives a fit where the points are weigthed 
y(x)= m*x+b
m= 0.01
b= 3
fit y(x) f u ($1*is):($2*xs):($3*xs) via m,b

# This gives a fit where all points are weighed equally
y_1(x)=m_1*x+b_1
m_1=0.01
b_1=3
fit y_1(x) f u ($1*is):($2*xs) via m_1,b_1

set xl "\\sf Intensity (kW/cm$\\sf ^{2}$)" offset 0,-0.6 
set yl "\\sf Light shift (MHz)" 5.25,0

set sample 101
set parametric
set trange[0:920]
set table 'filled.tab'
set iso 2
splot [t=0:920] t,(m_1-1.414*m_1_err)*t,(m_1+1.414*m_1_err)*t
unset table
unset parametric 

set xrange[0:920]
set yrange[:0.9]

set xtics format ""
set xtics 0,200,1000
set mxtics 2
set xtics add ("\\sf 0" 0, "\\sf 200" 200, "\\sf 400" 400, "\\sf 600" 600, "\\sf 800" 800) 

set ytics format ""
set ytics 0,0.2,0.8
set ytics add ("\\sf 0" 0, "\\sf 0.2" 0.2, "\\sf 0.4" 0.4, "\\sf 0.6" 0.6, "\\sf 0.8" 0.8)

set style fill solid 0.4 noborder
shadecolor = "#000000"

l1=sprintf("\\sf %.0f $\\sf \\pm$ %.0f $\\sf \\frac{\\mathsf{Hz}}{ \\mathsf{kW}/\\mathsf{cm}^{\\sf 2}}$", m_1*1.e6, 140)
print l1
set label l1 at 70,0.7

set terminal epslatex size 4.2,3.3 lw 3
set size 1,0.8
set output "lightshift_retake.tex"

set arrow from 906,0.8 to 920,0.8 nohead front lw 0.88

plot \
"filled.tab" u ($1):2:3 w filledcurve below  notitle,\
f u ($1*is):($2*xs-b):($3*xs) w yerrorbars ps 2 pt 7 lt 1 notitle ,\
m_1*x lt 1 lc rgb "#444444" notitle 
#(m_1+m_1_err)*x notitle ,\
#(m_1-m_1_err)*x notitle 





