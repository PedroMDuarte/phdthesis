reset
unset multiplot

set terminal postscript enhanced eps color solid font "Helvetica,16"
set output "lockfig.eps"


pi=4.*atan(1.0);
#lorentz(x)= a*g/( (2*pi*(x-c))**2 + (pi*g)**2 ) + dc;
Dlorentz(x) = -(a*g/((2*pi*(x-c))**2 + (pi*g)**2)**2)*8*pi*(x-c) + o;
a=-100.;
g=5.8;
c=-0.01;
o=-0.01;

s= 97.628;
off=-0.938;
scale(x) = (x-off)*s;

range=30.;

fit [-range:range] Dlorentz(x) "trace.dat" using (scale($1)):2 via a,g,c
leg1 = sprintf("LW = %.1f MHz", g)
set label 1 "LW = 5.8 +/- 0.075 MHz"  at c+4,0.1


f(x)=m*x+b
m=-0.00066;
b=1.9;
#fit [-0.1:0.1] f(x) "trace.dat"  using (scale($1)):2 via m,b
#set arrow from second 100.*s,f(100.) to second 600.*s,f(600.) nohead
slope = sprintf("%.2f mV/MHz", m*1000)
#set label 2 slope at 56,0.11

set samples 1000
#set title "lock error signal"

set xlabel "Detuning [MHz]"
set ylabel "Lock error signal [V]"
#set y2label "FP piezo control [V]"
set size ratio 0.8

set ytics nomirror
#set y2tics
set key top left
set xr [-range:range]
set multiplot
set size 1,1
set origin 0,0

set label 6 "Locked linewidth:" at 7,0.07
set label 4 "pk-pk: 1.3 MHz" at 7,0.05
set label 5 "rms  :  210 kHz" at 7,0.03

plot \
"trace.dat" using (scale($1)):2 title 'Trap', \
Dlorentz(x) lw 2 notitle
#"trace.dat" using (scale($1)):2 axes x1y2 title 'FP sweep', \
#f(x) axes x1y2 notitle

unset ylabel
unset y2label
unset xlabel
unset key 
unset label 1
unset label 2
unset label 4
unset label 5
unset label 6
unset y2tics
set xtics autofreq 0.3
set ytics autofreq 0.05
set size 0.4,0.4
set origin 0.5,0.1

xrange = 0.6;
yrange = 0.1;

set xr [-xrange:xrange]
set yr [-yrange:yrange]

set title "Error signal zero crossing"

lock(x)= err*(x-x0);
err=-0.1;
x0=0.01;

fit [-xrange:xrange] lock(x) "trace.dat" using (scale($1)):2 via err,x0
errslope = sprintf("%.2f\nmV/MHz", err*1000)
set label 3 errslope at 0,-0.05



plot \
"trace.dat" using (scale($1)):2 notit, \
Dlorentz(x) lw 2 lt rgb "green" title 'trap feature', \
lock(x) notit

unset label 3
set size 0.4,0.4
set origin 0.1 ,0.1

#unset xtics
set ytics autofreq 50
set xr [-xrange:xrange]
set yr [-100:100]

set title "Error [mV], {/Symbol D}t=1 s"


plot \
"locked.dat" using 1:($2*1000) notit


#set terminal X11

