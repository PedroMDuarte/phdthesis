reset 
set fit errorvariables
set fit logfile "mylog.fit"
f = "7334_7340.TOFDAT"
kbm=13.85e-06 #kb/m in cm^2/ms^2/uK
rH(x) = x>0?sqrt(r0H**2+2*kbm*TH*x**2):r0H
r0H = 0.3
TH  = 100.
fit rH(x) f u 1:($4) via r0H,TH
rV(x) = x>0?sqrt(r0V**2+2*kbm*TV*x**2):r0V
r0V = 0.1
TV  = 100.
fit rV(x) f u 1:($5) via r0V,TV
f2 = "<awk ' !/#/ {print $1, 0.5*($4*$4+$5*$5)}' 7334_7340.TOFDAT"
rsq_ave(x) = rsq_ave0 + 2*kbm*T_ave*x**2
rsq0_ave = 0.01
T_ave = 100.0
fit rsq_ave(x) f2 u 1:2 via rsq_ave0, T_ave
N(x) = N0
N0 = 4e8
fit [0:3.0] N(x)  f u 1:2 via N0
pi=4.*atan(1.0)
n(N,r) = N/r**3./pi**(3./2.)
n_err(N,r,N_err,r_err) = n(N,r)*( (N_err/N)**2 + (3*r_err/r)**2   )**0.5
rho(n,T) = n*(5.07e-9/T)**(3./2.)
rho_err(n,T,n_err,T_err) =  rho(n,T)*( (n_err/n)**2 + (1.5*T_err/T)**2  )**0.5
n0 = n(N0,r0H)
n0_err = n_err(N0,r0H,N0_err,r0H_err)
p0 = rho(n0,TH)
p0_err = rho_err(n0,TH,n0_err,TH_err)
print sprintf("%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3f\t%.3e\t%.3e\t%3e\t%.3e\t%.3e\t%.3e", TH, TH_err, r0H, r0H_err, TV, TV_err, r0V, r0V_err, T_ave, T_ave_err, N0, N0_err, n0, n0_err, p0, p0_err)
 plot \
f u 1:($4) ps 1.5 lc rgbcolor "red" title "H 7334 - 7340" ,\
rH(x) notitle lc rgbcolor "red",\
f u 1:($5) ps 1.5 lc rgbcolor "blue" title "V 7334 - 7340" ,\
rV(x) notitle lc rgbcolor "blue",\
sqrt(rsq_ave(x)) notitle lc rgbcolor "black"
