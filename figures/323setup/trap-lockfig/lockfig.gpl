reset
unset multiplot

set terminal epslatex size 4.5,3.0 lw 2 
set output "lockfig.tex"


pi=4.*atan(1.0);
#lorentz(x)= a*g/( (2*pi*(x-c))**2 + (pi*g)**2 ) + dc;
Dlorentz(x) = -(a*g/((2*pi*(x-c))**2 + (pi*g)**2)**2)*8*pi*(x-c) + o;
a=-100.;
g=5.8;
c=-0.01;
o=-0.01;

s= 97.628;
off=-0.938;
scale(x) = (x-off)*s;

range=30.;

fit [-range:range] Dlorentz(x) "trace.dat" using (scale($1)):2 via a,g,c
leg1 = sprintf("LW = %.1f MHz", g)
set label 1 "\\sf \\small 5.6$\\pm$0.1 MHz"  at c+8,0.12


f(x)=m*x+b
m=-0.00066;
b=1.9;
#fit [-0.1:0.1] f(x) "trace.dat"  using (scale($1)):2 via m,b
#set arrow from second 100.*s,f(100.) to second 600.*s,f(600.) nohead
slope = sprintf("%.2f mV/MHz", m*1000)
#set label 2 slope at 56,0.11

set samples 1000
#set title "lock error signal"

set xlabel "\\sf Detuning (MHz)"
set ylabel "\\sf Lock error signal (V)" offset 1,0
#set y2label "FP piezo control (V)"
set size ratio 0.8

set ytics nomirror
#set y2tics
set key top left
set xr [-range-20:range+20]
set yr [-0.3:0.2]
set multiplot
set size 1,1
set origin 0,0

set label 6 "\\sf \\small Locked linewidth:" at -40,0.11
set label 4 "\\sf \\small pk-pk: 1.3 MHz" at -40,0.07
set label 5 "\\sf \\small rms  :  210 kHz" at -40,0.04

plot \
"trace.dat" using (scale($1)):2 ps 0.5 notitle, \
Dlorentz(x) lw 2 notitle
#"trace.dat" using (scale($1)):2 axes x1y2 title 'FP sweep', \
#f(x) axes x1y2 notitle

unset ylabel
unset y2label
unset xlabel
unset key 
unset label 1
unset label 2
unset label 4
unset label 5
unset label 6
unset y2tics

#set xtics autofreq 0.3
#set ytics autofreq 0.05
set ytics offset 0.5,0 ( "\\tiny -100" -100, "\\tiny -50" -50, "\\tiny 0" 0, "\\tiny 50" 50, "\\tiny 100" 100)
set xtics offset 0,0.5 ( "\\tiny -0.5" -0.5, "\\tiny 0" 0, "\\tiny 0.5" 0.5) 

set yl "\\sf \\tiny mV" offset  10.5,0
set xl "\\sf \\tiny MHz" offset 0,1.5

set size 0.54,0.52
set origin 0.37,0.14

xrange = 0.6;
yrange = 100;

set xr [-xrange:xrange]
set yr [-yrange:yrange]

set title "\\sf \\tiny Error signal zero crossing" offset 0,-0.45

lock(x)= err*(x-x0);
err=-0.1;
x0=0.01;

fit [-xrange:xrange] lock(x) "trace.dat" using (scale($1)):2 via err,x0
errslope = sprintf("\\sf \\tiny %.2f mV/MHz", err*1000)
set label 3 errslope at -0.4,120


plot \
"trace.dat" using (scale($1)):($2*1000) ps 1.0 notitle, \
lock(x)*1000 notitle
#Dlorentz(x) lw 2 lt rgb "green" notitle, \

unset xl
unset yl
unset label 3
set size 0.545,0.65
set origin 0.03 ,0.08

unset xtics
set ytics ("\\tiny -100" -100, "\\tiny -50" -50, "\\tiny 0" 0, "\\tiny 50" 50, "\\tiny 100" 100)
#set ytics autofreq 50
set xr [-xrange:xrange]
set yr [-100:100]

set title "\\sf \\tiny Error (mV), $\\Delta$ t=1 s" offset 0,-1


plot \
"locked.dat" using 1:($2*1000) ps 0.5 notit


#set terminal X11

