reset

pi=4.*atan(1.0);
#lorentz(x)= a*g/( (2*pi*(x-c))**2 + (pi*g)**2 ) + o;
Dlorentz(x) = -(a*g/((2*pi*(x-c))**2 + (pi*g)**2)**2)*8*pi*(x-c) + o;
a=1.0;
g=2.0;
c=0.01;
o=-0.32;
fit [-50:10] Dlorentz(x) "scaled.dat" using 1:2 via a,g,c,o

#lorentz2(x)= a2*g2/( (2*pi*(x-c2))**2 + (pi*g2)**2 ) + o2;
Dlorentz2(x) = -(a2*g2/((2*pi*(x-c2))**2 + (pi*g2)**2)**2)*8*pi*(x-c2) + o2;
a2=5.0;
g2=10.0;
c2=228.2;
o2=-0.32;
fit [180:280] Dlorentz2(x) "scaled.dat" using 1:2 via a2,g2,c2,o2

#note that crossover is inverted so get rid of the minus sign!
Dlorentz3(x) = (a3*g3/((2*pi*(x-c3))**2 + (pi*g3)**2)**2)*8*pi*(x-c3) + o3;
a3=5.0;
g3=10.0;
c3=120;
o3=-0.32;
fit [50:170] Dlorentz3(x) "scaled.dat" using 1:2 via a3,g3,c3,o3

print c2-c

s= 228.2/(c2-c); 
#s=1

g1=g

fit [-50:50] Dlorentz(x) "scaled.dat" using ($1*s):2 via a,g,c,o
leg1 = sprintf("\\sf \\small %.1f MHz", g)
set label leg1 at -20,-0.12

fit [180:280] Dlorentz2(x) "scaled.dat" using ($1*s):2 via a2,g2,c2,o2
leg2 = sprintf("\\sf \\small %.1f MHz", g2)
set label leg2 at 90,-0.11

fit [70:170] Dlorentz3(x) "scaled.dat" using ($1*s):2 via a3,g3,c3,o3
leg3 = sprintf("\\sf \\small %.1f MHz", g3)
set label leg3 at 235,-0.16


set samples 1000
#set title "AOM modulation: 50 kHz, Chopper modulation: 1.5 kHz"

set xlabel "\\sf Detuning (MHz)"
set ylabel "\\sf Lock error signal (V)" offset 1,0

set xr [-50:300]
set xtics ("50" -50, "0" 0, "-50" 50, "-100" 100, "-150" 150, "-200" 200, "-250" 250, "-300" 300)

set label "\\sf \\small CROSSOVER" at 77,0.12
set label "\\sf \\small F=1/2" at -20,0.13 
set label "\\sf \\small F=3/2" at 236,0.155

set key top left

plot \
"scaled.dat" using ($1*s):(-1.*$2) ps 0.5 notitle ,\
x<50?-Dlorentz(x):1/0 lt 1 notitle ,\
x>200?-Dlorentz2(x):1/0 lt 1  notitle ,\
x>=50?x<=200?-Dlorentz3(x):1/0:1/0 lt 1 notitle 


set terminal epslatex size 4.5,3.0 lw 2
set size ratio 0.8
set output "errsig.tex"
replot

