reset
set fit errorvariables

f="/lab/data/app3/2011/1107/110711/uvdet.LSDAT"

xs=1.0 #x-scale is 1.0 MHz/ MHz
i0=654. #Full trap intensity in kW/cm^2

is= 0.66*884./654.

# This gives a fit where the points are weigthed by their errors
y(x)=m*x+b
m=0.01
b=200.5
fit y(x) f u ($1*is):($2*xs):($3*xs) via m,b

# This gives a fit where all points are weighed equally
y_1(x)=m_1*x+b_1
m_1=0.01
b_1=200.5
fit y_1(x) f u ($1*is):($2*xs) via m_1,b_1

set xl "\\sf Intensity (kW/cm$\\sf ^{2}$)" offset 0,0.25 
set yl "\\sf Light shift (MHz)" 5.0,0

set sample 101
set parametric
set trange[0:800]
set table 'filled.tab'
set iso 2
splot [t=0:800] t,(m_1-1.414*m_1_err)*t,(m_1+1.414*m_1_err)*t
unset table
unset parametric 

set xrange[0:600]
set yrange[:0.45]

set xtics ("\\sf 0" 0, "\\sf 100" 100, "\\sf 200" 200, "\\sf 300" 300, "\\sf 400" 400, "\\sf 500" 500, "\\sf 600" 600, "\\sf 700" 700) 
set ytics ("\\sf -0.1" 0.1, "\\sf 0" 0, "\\sf 0.1" 0.1, "\\sf 0.2" 0.2, "\\sf 0.3" 0.3, "\\sf 0.4" 0.4, "\\sf 0.5" 0.5)

set style fill solid 0.1 noborder
shadecolor = "#000000"

l1=sprintf("\\sf %.2f $\\sf \\pm$ %.2f $\\sf \\frac{\\mathsf{kHz}}{ \\mathsf{kW}/\\mathsf{cm}^{\\sf 2}}$", m_1*1000., m_1_err*1000.)
print l1
#set label l1 at 70,0.39

set terminal epslatex size 4.2,3.3 lw 3
set size 1,0.8
set output "lightshift.tex"


plot \
"filled.tab" u ($1):2:3 w filledcurve  notitle,\
f u ($1*is):($2*xs-b):($3*xs) w yerrorbars ps 2 pt 7 lt 1 notitle ,\
m_1*x lt 1 lc rgb "#444444" notitle 
#(m_1+m_1_err)*x notitle ,\
#(m_1-m_1_err)*x notitle 





